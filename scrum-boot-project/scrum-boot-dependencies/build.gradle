plugins {
    id 'java-platform'
}

description = "Scrum Boot Framework (Bill of Materials)"

javaPlatform {
    allowDependencies()
}

ext {
    set('mybatisPlusVersion', '3.5.1')
    set('springdocVersion', '1.6.7')
    set('guavaVersion', '31.1-jre')
    set('velocityVersion', '2.3')
    set('tinyPinyVersion', '2.0.3')
    set('zxingVersion', '3.4.1')
    set('cglibVersion', '3.3.0')
    set('flowableVersion', '6.7.2')
    set('shardingsphereVersion', '5.1.1')
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    api platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")
    api platform("com.alibaba.cloud:spring-cloud-alibaba-dependencies:$alibabaCloudVersion")
    api platform("com.google.zxing:zxing-parent:3.4.1")
    constraints {
        api "org.apache.shardingsphere:shardingsphere-jdbc-core-spring-boot-starter:$shardingsphereVersion"
        api "com.baomidou:mybatis-plus-boot-starter:$mybatisPlusVersion"
        api "com.baomidou:mybatis-plus-generator:$mybatisPlusVersion"
        api "com.baomidou:mybatis-plus:$mybatisPlusVersion"
        api "org.flowable:flowable-spring-boot-starter:$flowableVersion"
        api "org.springdoc:springdoc-openapi-ui:$springdocVersion"
        api "org.springdoc:springdoc-openapi-security:$springdocVersion"
        api "com.google.guava:guava:$guavaVersion"
        api "org.apache.velocity:velocity-engine-core:${velocityVersion}"
        api "com.github.promeg:tinypinyin:$tinyPinyVersion"
        api "cglib:cglib:$cglibVersion"
        rootProject.subprojects {pro ->
            pro.plugins.withType(JavaPlugin).whenPluginAdded {
                api pro
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform
        }
    }
}